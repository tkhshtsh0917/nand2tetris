// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    Not(in=instruction[15], out=isAcommand);

    Or(a=isAcommand, b=instruction[5], out=writeA);
    And(a=instruction[15], b=instruction[4], out=writeD);
    And(a=instruction[15], b=instruction[3], out=writeM);

    Mux16(a=outALUtoA, b=instruction, sel=isAcommand, out=inAreg);
    
    ARegister(in=inAreg, load=writeA, out=outAreg, out[0..14]=addressM);

    And(a=instruction[15], b=instruction[12], out=inMux);
    Mux16(a=outAreg, b=inM, sel=inMux, out=inALU);

    DRegister(in=outALUtoD, load=writeD, out=outDreg);

    ALU(
        x=outDreg,
        y=inALU,
        zx=instruction[11],
        nx=instruction[10],
        zy=instruction[9],
        ny=instruction[8],
        f=instruction[7],
        no=instruction[6],
        out=outM,
        out=outALUtoA,
        out=outALUtoD,
        zr=zr,
        ng=ng
    );

    Or(a=ng, b=zr, out=ngorzr);
    Not(in=ngorzr, out=ps);
    And(a=instruction[2], b=ng, out=jumpne);
    And(a=instruction[1], b=zr, out=jumpzr);
    And(a=instruction[0], b=ps, out=jumpps);
    Or(a=jumpne, b=jumpps, out=jump1);
    Or(a=jump1,  b=jumpzr, out=jump2);
    And(a=jump2, b=instruction[15], out=jump);
    Not(in=jump, out=inc);

    PC(in=outAreg, load=jump, inc=inc, reset=reset, out[0..14]=pc);
}
